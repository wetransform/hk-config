import "./Builtins.pkl"
import "./Config.pkl"

local ci_check_profile = "ci-check-all"
local not_ci_check_profile = "!\(ci_check_profile)"

actionlint = (Builtins.actionlint) {
  // extend to include actiion.yml/yaml files XXX turned out this is not supported by actionlint at the moment
  // glob = List(".github/workflows/*.yml", ".github/workflows/*.yaml", "action.yml", "action.yaml")

  prefix = "mise x actionlint --"

  // exclude managed tf- files which are generated
  exclude = List(".github/workflows/tf-*")
}

prettier = (Builtins.prettier) {
  prefix = "mise x prettier --"

  // batching seems to cause problems in combination with using `mise x`
  batch = false

  // exclude files which are generated/managed
  exclude = List(".github/workflows/tf-*", "CHANGELOG.md")
}

// renovate: datasource=github-tags depName=apple/pkl
local pkl_version = "0.30.1"
local pkl_prefix = "mise x pkl@\(pkl_version) --"

pkl = (Builtins.pkl) {
  prefix = pkl_prefix
}

// Note: supported since pkl 0.30 - https://pkl-lang.org/main/current/release-notes/0.30.html
pklformat = (Builtins.pkl_format) {
  prefix = pkl_prefix
}

spotlessGradle = new Config.Step {
  // define relevant file types for default Spotless configuration to avoid unnecessary runs
  glob = List("*.gradle", "*.java", "*.scala", "*.sc", "*.groovy", "*.kt", "*.kts", "*.md")

  check = new Config.Script {
    linux = "./gradlew spotlessCheck"
    macos = "./gradlew spotlessCheck"
    windows = "gradlew.bat spotlessCheck"
  }

  fix = new Config.Script {
    linux = "./gradlew spotlessApply"
    macos = "./gradlew spotlessApply"
    windows = "gradlew.bat spotlessApply"
  }

  // Only run if Gradle wrapper exists - FIXME does this work with Windows?
  // condition = "test -d gradle/wrapper" FIXME does not actually work like this - based on outdated example
  //TODO reevaluate at a later time if we can add such a condition, for now we assume if this is imported, the project uses Gradle
}

// Secret detection tools

// https://github.com/Yelp/detect-secrets
detectsecrets = new Config.Step {
  // needs a baseline
  // create with `detect-secrets scan > .secrets.baseline`
  // check will fail if file does not exist
  check = "detect-secrets-hook --baseline .secrets.baseline {{files}}"

  prefix = "mise x pipx:detect-secrets --"
}

// https://github.com/sirwart/ripsecrets
ripsecrets = new Config.Step {
  check = "ripsecrets {{files}}"
  prefix = "mise x ripsecrets --"
}

// don't use latest gitleaks version as there may be breaking changes (even for minor version updates)
// renovate: datasource=github-tags depName=gitleaks/gitleaks
local gitleaks_version = "8.30.0"
local gitleaks_prefix = "mise x gitleaks@\(gitleaks_version) --"

// https://github.com/gitleaks/gitleaks
gitleaks = new Config.Step {
  // XXX scans whole folder if there are multiple files (see https://github.com/gitleaks/gitleaks/issues/1727)
  // apart from unnecessary performance impact, this also causes files that are in .gitignore to be scanned
  // check = "gitleaks dir -v {{files}}"
  // workaround: run for each file individually - this has a big performance impact though
  // Note: running with individual files it does not pick up .gitleaks.toml automatically, so we need to specify it if it exists
  check =
    "sh -c 'set -e; for file in \"$@\"; do echo \"Scanning file $file\"; if [ -f .gitleaks.toml ]; then gitleaks dir --config .gitleaks.toml --no-banner -v \"$file\"; else gitleaks dir --no-banner -v \"$file\"; fi; done' _ {{files}}"
  prefix = gitleaks_prefix
  // also show stdout to include info on scanned file and found secrets in the output
  output_summary = "combined"
  // run checks in parallel for many files
  // Note: gain depends on number of CPU threads
  batch = true
  // Do not run on CI check where all files are checked together, because of bad performance
  // Note: instead, use gitleaks_git step below to scan the git history in CI
  profiles = List(not_ci_check_profile)
}

gitleaks_git = new Config.Step {
  profiles = List(ci_check_profile)
  // perform a check based on git, also taking history into account
  check = "gitleaks git -v --redact"
  prefix = gitleaks_prefix
}

// https://github.com/trufflesecurity/trufflehog
trufflehog = new Config.Step {
  check = "trufflehog filesystem {{files}} --fail"
  prefix = "mise x trufflehog --"
}

// https://trivy.dev/docs/latest/scanner/secret/
trivysecrets = new Config.Step {
  // Note: trivy fs can only take one file/path as argument, so we can only scan the current directory and not pass the files
  check = "trivy fs --exit-code 1 --scanners secret ."
  prefix = "mise x trivy --"
}
