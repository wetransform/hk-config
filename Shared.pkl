import "./Builtins.pkl"
import "./Config.pkl"

actionlint = (Builtins.actionlint) {
  // extend to include actiion.yml/yaml files XXX turned out this is not supported by actionlint at the moment
  // glob = List(".github/workflows/*.yml", ".github/workflows/*.yaml", "action.yml", "action.yaml")

  // exclude managed tf- files which are generated
  exclude = List(".github/workflows/tf-*")
}

prettier = (Builtins.prettier) {
  // exclude files which are generated/managed
  exclude = List(".github/workflows/tf-*", "CHANGELOG.md")
}

pkl = new Config.Step {
  glob = "*.pkl"
  check = new Config.Script {
    linux = "pkl eval {{files}} >/dev/null"
    macos = "pkl eval {{files}} >/dev/null"
    windows = "pkl eval {{files}} >nul"
  }
}

// Note: supported since pkl 0.30 - https://pkl-lang.org/main/current/release-notes/0.30.html
pklformat = new Config.Step {
  glob = "*.pkl"
  check = "pkl format --silent {{files}}"
  // exit code of pkl format is 1 if changes were made, so we first attempt to write changes with -w, and if that fails, we run again with --silent to ensure a zero exit code on success
  fix = "pkl format -w {{files}} || pkl format --silent {{files}}"
}

spotlessGradle = new Config.Step {
  // define relevant file types for default Spotless configuration to avoid unnecessary runs
  glob = List("*.gradle", "*.java", "*.scala", "*.sc", "*.groovy", "*.kt", "*.kts", "*.md")

  check = new Config.Script {
    linux = "./gradlew spotlessCheck"
    macos = "./gradlew spotlessCheck"
    windows = "gradlew.bat spotlessCheck"
  }

  fix = new Config.Script {
    linux = "./gradlew spotlessApply"
    macos = "./gradlew spotlessApply"
    windows = "gradlew.bat spotlessApply"
  }

  // Only run if Gradle wrapper exists - FIXME does this work with Windows?
  // condition = "test -d gradle/wrapper" FIXME does not actually work like this - based on outdated example
  //TODO reevaluate at a later time if we can add such a condition, for now we assume if this is imported, the project uses Gradle
}

// Secret detection tools

// https://github.com/Yelp/detect-secrets
// mise: pipx:detect-secrets
detectsecrets = new Config.Step {
  // needs a baseline
  // create with `detect-secrets scan > .secrets.baseline`
  // check will fail if file does not exist
  check = "detect-secrets-hook --baseline .secrets.baseline {{files}}"
}

// https://github.com/sirwart/ripsecrets
ripsecrets = new Config.Step {
  check = "ripsecrets {{files}}"
}

// https://github.com/gitleaks/gitleaks
gitleaks = new Config.Step {
  // XXX scans whole folder if there are multiple files (see https://github.com/gitleaks/gitleaks/issues/1727)
  check = "gitleaks dir -v {{files}}"
}

// https://github.com/trufflesecurity/trufflehog
trufflehog = new Config.Step {
  check = "trufflehog filesystem {{files}} --fail"
}

// https://trivy.dev/docs/latest/scanner/secret/
trivysecrets = new Config.Step {
  // Note: trivy fs can only take one file/path as argument, so we can only scan the current directory and not pass the files
  check = "trivy fs --exit-code 1 --scanners secret ."
}
